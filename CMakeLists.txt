CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT(pv_opensg_plugin)
SET(VERSION_MAJOR 1)
SET(VERSION_MINOR 0)

SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}")
FIND_PACKAGE(ParaView REQUIRED)
FIND_PACKAGE(OpenSG COMPONENTS OSGBase OSGSystem REQUIRED)
INCLUDE(${PARAVIEW_USE_FILE})

IF("${PARAVIEW_VERSION_MAJOR}.${PARAVIEW_VERSION_MINOR}" VERSION_LESS "3.10")
  SET(SERVER_MANAGER_XML_FILE OpenSGExporter_3_8_1.xml)
ELSE()
  SET(SERVER_MANAGER_XML_FILE OpenSGExporter.xml)
ENDIF() # PARAVIEW_VERSION

# Initing and exiting of OpenSG
QT4_WRAP_CPP(MOC_SRCS pqOsgStarter.h)
ADD_PARAVIEW_AUTO_START(IFACES IFACE_SRCS CLASS_NAME pqOsgStarter
                        STARTUP onStartup
                        SHUTDOWN onShutdown)

ADD_PARAVIEW_PLUGIN(OpenSG_Exporter "${VERSION_MAJOR}.${VERSION_MINOR}"
	GUI_INTERFACES ${IFACES}
	SERVER_MANAGER_XML ${SERVER_MANAGER_XML_FILE}
	SERVER_MANAGER_SOURCES vtkOpenSGExporter.cxx
	SERVER_SOURCES vtkOSGConverter.cpp
	SOURCES pqOsgStarter.cxx ${MOC_SRCS} ${IFACE_SRCS}
	)

USE_OPENSG(OpenSG_Exporter)


### INSTALL and PACKAGING ###
STRING(REPLACE ".lib" ".dll" OpenSG_OSGBASE_LIBRARY_DLL ${OpenSG_OSGBASE_LIBRARY})
STRING(REPLACE ".lib" ".dll" OpenSG_OSGSYSTEM_LIBRARY_DLL ${OpenSG_OSGSYSTEM_LIBRARY})

# Install targets with an absolute destination path will not be packaged at all!!!
OPTION(PACKAGING "This will package everything as a zip file." OFF)
IF(PACKAGING)
  SET(INSTALL_DIRECTORY .)
ELSE()
  SET(INSTALL_DIRECTORY ${ParaView_DIR}/bin)
ENDIF() # PACKAGING

IF(WIN32)
	INSTALL(TARGETS OpenSG_Exporter RUNTIME DESTINATION ${INSTALL_DIRECTORY})
	INSTALL(FILES
	  ${OpenSG_OSGBASE_LIBRARY_DLL}
	  ${OpenSG_OSGSYSTEM_LIBRARY_DLL}
	  ${OpenSG_INCLUDE_DIR}/../../../../../supportlibs/lib/zlib.dll
	  ${CMAKE_SOURCE_DIR}/README.md
	  DESTINATION ${INSTALL_DIRECTORY})
	
	IF(PACKAGING)
	  SET(CPACK_GENERATOR ZIP)
	  SET(CPACK_PACKAGE_VERSION_MAJOR "${VERSION_MAJOR}")
    SET(CPACK_PACKAGE_VERSION_MINOR "${VERSION_MINOR}")
    SET(CPACK_PACKAGE_VERSION_PATCH "0")
    SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "ParaView OpenSG-Exporter Plugin")
    SET(CPACK_PACKAGE_VENDOR "Lars Bilke")
    SET(CPACK_PACKAGE_INSTALL_DIRECTORY ".")
    IF(${CMAKE_SIZEOF_VOID_P} EQUAL 8)
      SET(WIN_VERSION "win64")
    ELSE()
      SET(WIN_VERSION "win32")
    ENDIF()
    SET(CPACK_PACKAGE_FILE_NAME "pv_opensg_plugin-${VERSION_MAJOR}.${VERSION_MINOR}-${WIN_VERSION}-ParaView-${PARAVIEW_VERSION_MAJOR}.${PARAVIEW_VERSION_MINOR}.${PARAVIEW_VERSION_PATCH}")
    SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_SOURCE_DIR}/README.md")
    INCLUDE(CPack)
  ENDIF() # PACKAGING
ENDIF() # WIN32